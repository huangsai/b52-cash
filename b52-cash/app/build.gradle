apply from: "../gradle/android_app_module.gradle"

android {

    defaultConfig {
        applicationId "com.mobile.app.cash"
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86','arm64-v8a'
            universalApk false
        }
    }

    signingConfigs {
        debug {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                String date = new Date().format("yyyyMMdd")
                String abiName = output.getFilter("ABI")
                if (abiName == null || abiName.isEmpty()) {
                    abiName = "all"
                }
                outputFileName = "app-${variant.versionName}-${abiName}-${date}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":app-core")
    implementation project(":bundle-im")
    implementation project(":bundle-main")
    implementation project(":bundle-payment")
    implementation project(":bundle-authenticator")
}